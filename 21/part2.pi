% curiously, the "sat" module produces an incorrect result (and in fact segfaults on my input, maybe due to the
% size of the domain variables)
import cp.
import util.

main =>
  Lines = read_file_lines(),
  Monkeys = map(parse_line, Lines),

  T = new_map(),
  foreach(M in Monkeys)
    T.put(M[1], M.drop(1))
  end,

  crunch(T).

crunch(T) =>
  Root = T.get("root"),
  % I'm using "P" to recursively tie together the expression tree.
  calc("root",Root,T,X,nop),
  println(x1=X),
  % cp somehow figures this one out even if we don't call "solve"???
  % sat doesn't, and throws a "free_var_not_allowed" error if I don't manually provide the domain of X
  % cp and mip return a correct answer incredibly fast (around 10 ms)
  % X :: 301..301,
  solve([split], X),
  println(x2=X).

calc("humn", _, T, X, P) =>
  println(humn=X),
  P :: 0..10000000000000,
  P #= X.
calc(Name, [K], T, X, P) =>
  Ki = K.to_int(),
  P #= Ki.
calc("root", [A,_,B], T, X, _) =>
  Am = T.get(A),
  Bm = T.get(B),
  calc(A,Am,T,X,P1),
  calc(B,Bm,T,X,P2),
  P1 #= P2.
calc(Name, [A,"+",B], T, X, P) =>
  Am = T.get(A),
  Bm = T.get(B),
  calc(A,Am,T,X,L),
  calc(B,Bm,T,X,R),
  P #= L + R.
calc(Name, [A,"-",B], T, X, P) =>
  Am = T.get(A),
  Bm = T.get(B),
  calc(A,Am,T,X,L),
  calc(B,Bm,T,X,R),
  P #= L - R.
calc(Name, [A,"*",B], T, X, P) =>
  Am = T.get(A),
  Bm = T.get(B),
  calc(A,Am,T,X,L),
  calc(B,Bm,T,X,R),
  P #= L * R.
calc(Name, [A,"/",B], T, X, P) =>
  Am = T.get(A),
  Bm = T.get(B),
  calc(A,Am,T,X,L),
  calc(B,Bm,T,X,R),
  L mod R #= 0,  % this seems to be very important and allows cp to resolve everything without invoking the solver...
  P * R #= L.

parse_line(S) = split(S,": ").
