import sat.
import util.

main =>
  Lines = read_file_lines(),
  Blueprints = map(parse_line, Lines),

  TotalQuality = 0,
  println(Blueprints),
  foreach(I in 1..length(Blueprints))
    Blueprint = Blueprints[I],
    println(Blueprint),
    Geodes = calc(Blueprint),
    println(Geodes),
    TotalQuality := TotalQuality + I * Geodes,
  end,
  println(totalQuality=TotalQuality).

calc(Blueprint) = R =>
  N = 25,
  Actions = new_array(N),
  Actions :: 0..4,
  Actions[1] #= 0,

  % Ores
  Ore = new_array(N),
  Clay = new_array(N),
  Obsidian = new_array(N),
  Geode = new_array(N),
  [Ore, Clay, Obsidian, Geode] :: 0..255,
  Ore[1] #= 0,
  Clay[1] #= 0,
  Obsidian[1] #= 0,
  Geode[1] #= 0,

  % Robots
  OreR = new_array(N),
  ClayR = new_array(N),
  ObsidianR = new_array(N),
  GeodeR = new_array(N),
  [OreR, ClayR, ObsidianR, GeodeR] :: 0..N,
  OreR[1] #= 1,
  ClayR[1] #= 0,
  ObsidianR[1] #= 0,
  GeodeR[1] #= 0,

  foreach(I in 2..N)
    % actions: 0 = wait, 1 = ore robot, 2 = clay robot, 3 = obsidian robot, 4 = geode robot
    Action #= Actions[I],

    % restrict actions requiring ore
    Ore[I-1] #< Blueprint[1] #=> Action #!= 1,
    Ore[I-1] #< Blueprint[2] #=> Action #!= 2,
    Ore[I-1] #< Blueprint[3] #=> Action #!= 3,
    Ore[I-1] #< Blueprint[5] #=> Action #!= 4,
    % restrict actions requiring clay
    Clay[I-1] #< Blueprint[4] #=> Action #!= 3,
    % restrict actions requiring obsidian
    Obsidian[I-1] #< Blueprint[6] #=> Action #!= 4,

    % update ores
    [OreCost, ClayCost, ObsidianCost] :: 0..40,
    Action #= 0 #=> OreCost #= 0,
    Action #= 1 #=> OreCost #= Blueprint[1],
    Action #= 2 #=> OreCost #= Blueprint[2],
    Action #= 3 #=> OreCost #= Blueprint[3],
    Action #= 4 #=> OreCost #= Blueprint[5],
    Ore[I] #= Ore[I-1] + OreR[I-1] - OreCost,

    Action #!= 3 #=> ClayCost #= 0,
    Action #= 3 #=> ClayCost #= Blueprint[4],
    Clay[I] #= Clay[I-1] + ClayR[I-1] - ClayCost,

    Action #!= 4 #=> ObsidianCost #= 0,
    Action #= 4 #=> ObsidianCost #= Blueprint[6],
    Obsidian[I] #= Obsidian[I-1] + ObsidianR[I-1] - ObsidianCost,

    Geode[I] #= Geode[I-1] + GeodeR[I-1],

    % update robots
    OreR[I] #= OreR[I-1] + cond(Action #= 1, 1, 0),
    ClayR[I] #= ClayR[I-1] + cond(Action #= 2, 1, 0),
    ObsidianR[I] #= ObsidianR[I-1] + cond(Action #= 3, 1, 0),
    GeodeR[I] #= GeodeR[I-1] + cond(Action #= 4, 1, 0),
  end,

  TotalScore #= Geode[25],
  solve([ff,split,$max(TotalScore)], Actions),
  println(totalScore=TotalScore),
  println(actions2=Actions),
  println(ore=Ore),
  println(oreR=OreR),
  println(geodes=Geode),
  println(geodeR=GeodeR),
  R = TotalScore.

parse_line(S) = R =>
  Xs = split(S),
  R = map(parse_term, [Xs[7],Xs[13],Xs[19],Xs[22],Xs[28],Xs[31]]).
